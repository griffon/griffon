/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2008-2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'maven-publish'
}

config {
    licensing { enabled = false }
    publishing { enabled = false }
    bintray { enabled = true }
    artifacts {
        minpom { enabled = false }
        source { enabled = false }
    }
    coverage {
        jacoco { enabled = false }
    }
    docs {
        javadoc { enabled = false }
        sourceHtml { enabled = false}
        sourceXref { enabled = false }
    }
    stats { enabled = false }
}

ext.pomFile = "${project.name}-${project.version}.xml".toString()

task copyPom(type: Copy) {
    destinationDir = file("${project.buildDir}/pom")
    from(file('src/pom')) {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'project.version': project.version,
            'groovy.version' : project.groovyVersion,
            'griffon.version': rootProject.version,
            'slf4j.version'  : slf4jVersion,
            'spock.version'  : spockVersion
        ])
    }
    rename(/.+\.xml/, pomFile)
}

publishing {
    publications {
        main(MavenPublication) {
            artifacts = []
            pom.withXml {
                def root = asNode()
                root.children().clear()
                def parsedPom = new XmlParser().parse(file("${project.buildDir}/pom/${project.pomFile}"))
                parsedPom.children().each { childNode -> root.append(childNode) }
            }
        }
    }
}

publishToMavenLocal.dependsOn copyPom
