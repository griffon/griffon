/*
 * Copyright 2016-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// tag::plugins[]
plugins {
    id 'java-library'
    id 'application'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.github.hierynomus.license' version '0.14.0'
}

mainClassName = 'org.example.Launcher'
// end::plugins[]

// tag::dependencies[]
repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    api "org.codehaus.griffon:griffon-javafx:${griffonVersion}"
    api "org.codehaus.griffon:griffon-guice:${griffonVersion}"
    api 'org.kordamp.ikonli:ikonli-javafx:2.1.1'
    api 'org.kordamp.ikonli:ikonli-fontawesome-pack:2.1.1'
    api 'org.kordamp.bootstrapfx:bootstrapfx-core:0.2.2'

    runtimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"

    testImplementation "org.codehaus.griffon:griffon-javafx-test:${griffonVersion}"
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testImplementation 'org.mockito:mockito-core:2.15.1'
}
// end::dependencies[]

// tag::resources[]
processResources {
    filesMatching(['**/*.properties','**/*.xml']) {
        expand(
            'application_name'   : project.name,
            'application_version': project.version,
            'griffon_version'    : griffonVersion
        )
    }
}
// end::resources[]

license {
    header = rootProject.file('gradle/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java = 'SLASHSTAR_STYLE'
        fxml = 'XML_STYLE'
    }
    ext.copyrightYear = '2016-2021'
    exclude '**/*.png'
}