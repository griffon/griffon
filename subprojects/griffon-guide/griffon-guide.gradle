/*
 * Copyright 2010-2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'java2html'

buildscript {
    repositories {
        add gitHub()
    }

    dependencies {
        classpath 'bmuschko:gradle-java2html-plugin:0.3'
    }
}

configurations {
    groovy
    docs { transitive = false }
}

repositories {
    mavenCentral()
}

dependencies {
    groovy localGroovy() //for api docs generation needed (groovydoc)

    // problems with ant classloader makes it necessary to duplicate the deps
    // in buildSrc/ and the docs configurations
    docs(localGroovy(),
         "org.grails:grails-docs:$grailsDocsVersion",
         'radeox:radeox:1.0-b2',
         'com.lowagie:itext:2.0.8',
         'org.xhtmlrenderer:core-renderer:R8pre2',
         'log4j:log4j:1.2.16',
         "org.slf4j:slf4j-api:$slf4jVersion",
         "org.slf4j:slf4j-log4j12:$slf4jVersion",
         "org.slf4j:jcl-over-slf4j:$slf4jVersion")

    java2html 'de.java2html:java2html:5.0'
}

task copyDocs(type: Copy) {
    destinationDir = "${buildDir}/sources" as File
    from(project(":griffon-cli").sourceSets.main.allSource) {
        include 'griffon/test/**/*'
        include 'griffon/transform/**/*'
    }
    from(project(":griffon-rt").sourceSets.main.allSource)
}

task apiDocs(type: Groovydoc, dependsOn: copyDocs) {
    source = "${buildDir}/sources" as File
    destinationDir = "$buildDir/api" as File
    docTitle = "Griffon $version"
    header = "Griffon $version"
    windowTitle = "Griffon $version"
    groovyClasspath = configurations.groovy
    classpath = project(":griffon-rt").sourceSets.main.output +
                project(":griffon-rt").configurations.compile

    includePrivate = true
    use = true

    link('http://java.sun.com/j2se/1.5.0/docs/api', 'java.,org.xml.,javax.,org.xml.')
    link('http://www.dpml.net/api/ant/1.7.0', 'org.apache.ant.,org.apache.tools.ant.')
    link('http://junit.sourceforge.net/junit3.8.1/javadoc/', 'org.junit.,junit.framework.')
    link('http://groovy.codehaus.org/api/', 'groovy.,org.codehaus.groovy.')

    doLast { task ->
        copy {
            into task.destinationDir
            from project(':').file('src/dist/media/griffon.ico'),
                 'src/guide/resources/css/stylesheet.css'
            // from('src/resources/javadoc') {
            //     into 'resources'
            // }
        }
        ant {
            replace(dir: task.destinationDir,
                    token: 'groovy.ico',
                    value: 'griffon.ico') {
                include(name: '**/*.html')
            }
        }
    }
}

task java2html(dependsOn: apiDocs) << {
    java2htmlConvertCode.execute()
    java2htmlGenerateOverview.execute()

}

task jarApi(type: Jar, dependsOn: apiDocs) {
    archiveName = "griffon-${project.version}-javadoc.jar"
    destinationDir = "$buildDir/assemble/jars" as File
    from "$buildDir/api"
}

task jarSource(type: Jar, dependsOn: copyDocs) {
    archiveName = "griffon-${project.version}-sources.jar"
    destinationDir = "$buildDir/assemble/jars" as File
    from "$buildDir/sources"
    exclude '**/*.html'
}

task jarSourceHtml(type: Jar, dependsOn: java2html) {
    archiveName = "griffon-${project.version}-sources-html.jar"
    destinationDir = "$buildDir/assemble/jars" as File
    from "$buildDir/java2html"
}

task jarDocs(dependsOn: [jarApi, jarSource, jarSourceHtml]) {}

task buildGuide(type: GrailsDoc, dependsOn: java2html) {
    srcDir = file('src/guide/src')
    props = file('src/guide/guide.properties')
    imagesDir = file('src/guide/resources/img')
    outputDir = file("$buildDir/manual")

    doFirst {
        copy {
            into("$buildDir/manual/api")
            from("$buildDir/api")
            into('src-html') {
                from("$buildDir/java2html")
            }
        }
    }

    doLast {
        ant {
            replace(dir: "$buildDir/manual") {
                replacefilter(token: '@griffon.version@', value: version)
            }
        }
    }
}

task pdfGuide(type:PdfGuide, dependsOn: buildGuide) {
    inputs.files file("$buildDir/manual/guide")
    pdfName = "griffon-guide-${version}.pdf"
}

task dist(type: Zip) {
    dependsOn { [pdfGuide, jarDocs] }
    from "$buildDir/manual"
}

java2html {
    conversion {
        srcDirs = files("${buildDir}/sources")
        destDir = file("${buildDir}/java2html")
        tabs = 4
        style = 'eclipse'
        outputFormat = 'html'
        showLineNumbers = true
        addLineAnchors = true
        showFileName = true
        useShortFileName = true
        overwrite = true
        showDefaultTitle = true
        includeDocumentHeader = true
        includeDocumentFooter = true
    }

    overview {
        srcDirs = files("${buildDir}/java2html")
        destDir = file("${buildDir}/java2html")
        windowTitle = "Griffon $version"
        docTitle = "Griffon $version"
        docDescription = """Griffon is a Grails like application framework for developing desktop
                            applications in Groovy. Inspired by Grails, Griffon follows the Convention over
                            Configuration paradigm, paired with an intuitive MVC architecture and a command
                            line interface."""
        header = "Griffon $version"
        icon = project(':').file('src/dist/media/griffon.ico')
        stylesheet = file('src/guide/resources/css/stylesheet.css')
    }
}
