/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2008-2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: rootProject.file('samples/shared-files/samples-common-java.gradle')

application {
    mainClassName = project.name.replace('-', '.') + '.Launcher'
}

ext.hasIntegrationTests = true
ext.hasFunctionalTests = false
ext.depProjects = [
    ':griffon-core',
    ':griffon-javafx',
    ':griffon-guice',
    ':griffon-core-test',
    ':griffon-javafx-test'
]

ext.depProjects.each { evaluationDependsOn it }
apply from: rootProject.file('gradle/coverage-samples.gradle')

config {
    coverage {
        jacoco {
            additionalSourceDirs.from project.files(depProjects.collect {
                project(it)
            }.sourceSets.main.allSource.srcDirs.flatten())
            additionalClassDirs.from depProjects.collect { project(it) }.sourceSets.main.output.classesDirs.flatten()
        }
    }
}

dependencies {
    api project(':griffon-javafx')
    compileOnly project(':griffon-javafx-compile')
    api "org.kordamp.ikonli:ikonli-javafx:$ikonliVersion"
    api "org.kordamp.ikonli:ikonli-fontawesome-pack:$ikonliVersion"
    api "org.kordamp.bootstrapfx:bootstrapfx-core:$bootstrapfxVersion"

    testImplementation project(':griffon-javafx-test')

    testImplementation "org.hamcrest:java-hamcrest:$hamcrestVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    testImplementation("org.junit.vintage:junit-vintage-engine:$junit5Version") {
        exclude module: 'junit', group: 'junit'
    }

    functionalTestCompile project(':griffon-javafx-test')
}

compileGroovy.enabled = false