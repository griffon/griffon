/*
 * Copyright 2008-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 * Bootstrap script for the Gradle JavaFX Plugin.
 * (based on http://plugins.jasoft.fi/vaadin.plugin)
 *
 * The script will add the latest version of the plugin to the build script
 * dependencies and apply the plugin to the project. If you do not want
 * this behavior you can copy and paste the below configuration into your
 * own build script and define your own repository and version for the plugin.
 */


buildscript {
    String jfxrtLocation = new File("${System.properties['java.home']}/jre/lib/jfxrt.jar").absolutePath
    // JavaFX locations for JDK7, JDK7, JDK8
    for (location in ['lib/jfxrt.jar', 'jre/lib/jfxrt.jar', 'jre/lib/ext/jfxrt.jar']) {
        File javaHome = new File(System.properties['java.home'])
        javaHome = javaHome.name == 'jre' ? javaHome.parentFile : javaHome
        File file = new File(javaHome, location)
        if (file.exists()) {
            jfxrtLocation = file.absolutePath
            break
        }
    }

    repositories {
        jcenter()
        maven { url = 'http://dl.bintray.com/content/shemnon/javafx-gradle/' }
        maven { url = 'http://repository-javafx-gradle-plugin.forge.cloudbees.com/snapshot' }
        ivy { url = 'http://repository-javafx-gradle-plugin.forge.cloudbees.com/snapshot' }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.bitbucket.shemnon.javafxplugin:gradle-javafx-plugin:0.4.0'
        classpath project.files("${System.properties['java.home']}/../lib/ant-javafx.jar")
        classpath project.files(jfxrtLocation)
    }
}

if (!project.plugins.findPlugin(org.bitbucket.shemnon.javafxplugin.JavaFXPlugin)) {
    project.apply(plugin: org.bitbucket.shemnon.javafxplugin.JavaFXPlugin)
}
