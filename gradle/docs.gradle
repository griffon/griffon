/*
 * Copyright 2010-2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        mavenRepo name: 'SpringSource', urls: 'http://repository.springsource.com/maven/bundles/release'
    }

    dependencies {
        classpath("org.grails:grails-docs:1.3.6") { transitive = false }
        classpath("org.xhtmlrenderer:core-renderer:R8pre2") { transitive = false }
        classpath("com.lowagie:itext:2.0.8") { transitive = false }
        classpath("radeox:radeox:1.0-b2") { transitive = false }
    }
}

task copyDocs(type: Copy) {
    destinationDir = "${buildDir}/javadoc-src" as File

    from('src/main/cli') {
        include 'griffon/test/**/*'
        include 'griffon/beans/**/*'
        include 'griffon/util/EventPublisher*'
        include '**/*.html'
    }
    from 'src/main/rt'
}

task buildDocs(type: Groovydoc) {
    dependsOn { copyDocs }

    source = files("$buildDir/javadoc-src")
    destinationDir = "$buildDir/manual/api" as File
    docTitle = "Griffon $version"
    header = "Griffon $version"
    windowTitle = "Griffon $version"
    groovyClassPath = sourceSets.rt.classes + sourceSets.cli.classes + configurations.compile
    includePrivate = true
    use = true

    link('http://java.sun.com/j2se/1.5.0/docs/api', 'java.,org.xml.,javax.,org.xml.')
    link('http://www.dpml.net/api/ant/1.7.0', 'org.apache.ant.,org.apache.tools.ant.')
    link('http://junit.sourceforge.net/junit3.8.1/javadoc/', 'org.junit.,junit.framework.')
    link('http://groovy.codehaus.org/api/', 'groovy.,org.codehaus.groovy.')
}

task jarApi(type: Jar) {
    dependsOn { buildDocs }

    classifier = 'javadoc'
    destinationDir = "$buildDir/assemble/jars" as File
    from "$buildDir/manual/api"
}

task jarSource(type: Jar) {
    dependsOn { copyDocs }

    classifier = 'sources'
    destinationDir = "$buildDir/assemble/jars" as File
    from "$buildDir/javadoc-src"
    exclude '**/*.html'
}

task jarDocs(dependsOn: [jarApi, jarSource]) {}

task prepareGuide(type: Copy) {
    destinationDir = "$buildDir/manual-src" as File
    from('src/guide') {
        filter(ReplaceTokens, tokens: ['griffon.version': version])
    }
}

task buildGuide(type: Copy) {
    dependsOn { [buildDocs, prepareGuide] }

    def manualSrc = "$buildDir/manual-src" as File
    destinationDir = "$buildDir/manual" as File
    from(manualSrc) {
        include '*.properties'
    }

    doLast {
        ant.taskdef(name: 'gdocs',
                    classname: 'grails.doc.ant.DocPublisherTask',
                    classpath: configurations.cli.asPath)
        ant.gdocs(src: "$manualSrc/src",
                  dest: destinationDir,
                  imagesdir: "$manualSrc/resources/img",
                  properties: "$destinationDir/guide.properties")
    }
}

task pdfGuide(type: Copy) {
    dependsOn { buildGuide }

    inputs.dir("$buildDir/manual" as File)
    outputs.files "$buildDir/manual/griffon-guide-${version}.pdf"

    doFirst {
        try {
            grails.doc.PdfBuilder.build(
                basedir: buildDir.absolutePath,
                home: project.file('.').absolutePath,
                tool: 'griffon'
            )
        } catch(x) {
            // it's very likely that the stream is closed before
            // the renderer 'finishes' but it actually does
            // ignore for now
        }
    }
        
    destinationDir = "$buildDir/manual" as File
    from "$destinationDir/guide/"
    into destinationDir
    include '*.pdf'
    rename 'single.pdf', "griffon-guide-${version}.pdf"

    doLast {
        delete "$destinationDir/guide/single.pdf"
    }
}

task zipGuide(type: Zip) {
    dependsOn { [pdfGuide, jarDocs] }
    appendix = 'guide'
    from "$buildDir/manual"
}
