/*
 * Copyright 2008-2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (!publishJars.toBoolean()) return

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.nemerosa.versioning'

def pomConfig = {
    name project.name
    description project.pomDescription
    url 'http://griffon-framework.org'
    inceptionYear '2008'
    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/griffon/griffon'
    }
    developers {
        [
            aalmiray: 'Andres Almiray',
            shemnon: 'Danno Ferrin',
            jwill: 'James Williams',
            aklein: 'Alexander Klein',
            rgroeschke: 'Rene Groeschke'
        ].each { devId, devName ->
            developer {
                id devId
                name devName
                roles {
                    role 'Developer'
                }
            }
        }
    }
    contributors {
        [
            'Dierk Koenig',
            'Guillaume Laforge',
            'Dean Iverson'
        ].each { cName ->
            contributor {
                name cName
                roles {
                    role 'contributor'
                }
            }
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourceJar

            pom.withXml {
                // all dependencies should use the default scope (compile) but
                // Gradle insists in using runtime as default
                asNode().dependencies.dependency.each { dep ->
                    if (dep.scope.text() == 'runtime') {
                        dep.remove(dep.scope)
                    }
                }
                asNode().children().last() + pomConfig
            }
        }
    }
}

jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
            'Build-Date': buildDate,
            'Build-Time': buildTime,
            'Build-Revision': versioning.info.commit,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': 'griffon-framework.org',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'griffon-framework.org'
        )
    }

    metaInf {
        from rootProject.file('.')
        include 'LICENSE'
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = 'griffon'
        userOrg = 'griffon'
        name = project.name
        desc = project.pomDescription
        licenses = ['Apache-2.0']
        labels = ['griffon']
        websiteUrl = 'http://griffon-framework.org'
        issueTrackerUrl = 'https://github.com/griffon/griffon/issues'
        vcsUrl = 'https://github.com/griffon/griffon.git'
        publicDownloadNumbers = true
    }
}