/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2008-2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        jcenter()
        maven { url 'https://dl.bintray.com/pledbrook/plugins' }
    }
    dependencies {
        classpath 'org.jbake:jbake-gradle-plugin:5.2.0'
        classpath 'org.ajoberstar:gradle-git-publish:2.1.3'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:5.0.3'
        classpath 'gradle.plugin.gradle-java9-collision:plugin:0.2.3'
        classpath 'gradle.plugin.net.ossindex:ossindex-gradle-plugin:0.4.11'
        classpath 'uk.co.cacoethes:lazybones-gradle:1.2.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.kordamp.gradle:jdeps-gradle-plugin:0.11.0"
        classpath "org.kordamp.gradle:java-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:groovy-project-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:guide-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:plugin-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:sonar-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:settings-gradle-plugin:$kordampPluginVersion"
        classpath "org.kordamp.gradle:enforcer-gradle-plugin:0.3.0"
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
    }
}
apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

boolean travis   = (System.env['TRAVIS'] ?: false) as boolean
boolean appveyor = (System.env['APPVEYOR'] ?: false) as boolean
boolean github   = (System.env['CI'] ?: false) as boolean
boolean fullBuild = !github && !travis && !appveyor

rootProject.name = 'griffon'

includeBuild '../jsr377-api'
includeBuild '../jsr377-tck'
includeBuild '../jsr377-converters'

projects {
    layout = 'explicit'

    includeFromDir('subprojects')
    includeFromDir('plugins')
    includeFromDir('samples')
        .exclude('editor-swing-groovy')
        .when(!appveyor)
    includeFromDir('poms').when(fullBuild)
    includeFromDir('templates').when(fullBuild)
    includeFromDir('docs').when(fullBuild)
    includeFromDir('archetypes').when(fullBuild)

    plugins {
        all {
            id 'idea'
            id('net.ossindex.audit').when(fullBuild)
        }
        path(':') {
            id 'org.kordamp.gradle.java-project'
            id 'org.kordamp.gradle.clirr'
            id 'org.kordamp.gradle.coveralls'
        }
        dir('subprojects') {
            id 'java-library'
            id 'groovy'
            id('nebula.optional-base')
            id('org.kordamp.gradle.jdeps').when({-> false && fullBuild})
            id('org.jonnyzzz.java9c').when(fullBuild)
        }
        dir('archetypes') {
            exclude('archetypes')
            id 'java-library'
        }
        dir('plugins') {
            id 'java-library'
            id 'groovy'
            id 'org.kordamp.gradle.groovy-project'
            id 'org.kordamp.gradle.plugin'
        }
        dir('samples') {
            id 'java-library'
            id 'application'
        }
        path(':sample-*') {
            id 'groovy'
            id 'org.kordamp.gradle.integration-test'
            id 'org.kordamp.gradle.functional-test'
        }
        path('*-groovy') {
            id 'groovy'
        }
        path('*-kotlin') {
            id 'kotlin'
        }
    }
}

enforce {
    enabled = false
    mergeStrategy = 'override'
    rule(enforcer.rules.DependencyConvergence)
    rule(enforcer.rules.BanDuplicateClasses) { r ->
        r.enabled = false
        // r.ignoreWhenIdentical = true
        // r.findAllDuplicates = false
        // dependency("org.slf4j:slf4j-log4j12:$slf4jVersion") { d ->
        //     d.ignore('org.slf4j.impl.*')
        // }
    }
    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "com.google.guava:guava:$guavaVersion",
            "jline:jline:$jlineVersion",
            "junit:junit:$junitVersion",
            "cglib:cglib-nodep:$cglibVersion",
            "org.asciidoctor:asciidoctorj:$asciidoctorjVersion",
            "org.codehau.groovy:groovy-all:$groovyVersion",
            "org.codehaus.groovy:groovy:$groovyVersion",
            "org.codehaus.groovy:groovy-json:$groovyVersion",
            "org.codehaus.groovy:groovy-macro:$groovyVersion",
            "org.codehaus.groovy:groovy-nio:$groovyVersion",
            "org.codehaus.groovy:groovy-sql:$groovyVersion",
            "org.codehaus.groovy:groovy-templates:$groovyVersion",
            "org.codehaus.groovy:groovy-test:$groovyVersion",
            "org.codehaus.groovy:groovy-xml:$groovyVersion",
            "org.slf4j:slf4j-api:$slf4jVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "com.google.code.findbugs:jsr305:$jsr305Version",
            "commons-io:commons-io:$commonsIoVersion",
            "commons-lang:commons-lang:$commonsLangVersion",
            "commons-logging:commons-logging:$commonsLoggingVersion",
            "org.apache.commons:commons-lang3:$commonsLang3Version",
            "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:$concurrentLHMVersion",
            "org.ow2.asm:asm:$asmVersion",
            "org.ow2.asm:asm-commons:$asmVersion",
            "org.ow2.asm:asm-tree:$asmVersion",
            "org.objenesis:objenesis:$objenesisVersion",
            "net.bytebuddy:byte-buddy:$bytebuddyVersion",
            "net.bytebuddy:byte-buddy-agent:$bytebuddyVersion",
            "org.junit.jupiter:junit-jupiter-engine:$junit5Version",
            "org.junit.platform:junit-platform-engine:$junitPlatformVersion",
            "org.junit.platform:junit-platform-launcher:$junitPlatformVersion",
            "org.apiguardian:apiguardian-api:$apiguardianVersion",
            "org.apache.ant:ant:$antVersion",
            "commons-collections:commons-collections:$commonsCollectionsVersion",
            "com.vladsch.flexmark:flexmark:$flexmarkVersion",
            "com.vladsch.flexmark:flexmark-profile-pegdown:$flexmarkVersion",
            "org.jruby:jruby-complete:$jrubyVersion",
            "org.eclipse.jgit:org.eclipse.jgit.ui:$jgitVersion",
            "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion",
            "com.jcraft:jsch.agentproxy.pageant:$jschAgentproxyVersion",
            "com.jcraft:jsch.agentproxy.sshagent:$jschAgentproxyVersion",
            "com.jcraft:jsch.agentproxy.usocket-jna:$jschAgentproxyVersion",
            "com.jcraft:jsch.agentproxy.usocket-nc:$jschAgentproxyVersion",
            "com.jcraft:jsch.agentproxy.core:$jschAgentproxyVersion",
            "com.jcraft:jsch:$jschVersion",
            "org.codehaus.plexus:plexus-utils:3.3.0",
            "org.codehaus.plexus:plexus-interpolation:1.26",
            "org.apache.ant:ant-launcher:1.9.13",
            "org.apache.maven:maven-settings:$mavenVersion",
            "org.apache.maven:maven-settings-builder:$mavenVersion",
            "org.codehaus.groovy.modules.http-builder:http-builder:0.7.2",
            "org.apache.httpcomponents:httpclient:4.5.12",
            "com.trilead:trilead-ssh2:1.0.0-build220",
            "org.apache.httpcomponents:httpcore:4.4.13",
            "org.eclipse.sisu:org.eclipse.sisu.plexus:0.3.4",
            "org.codehaus.plexus:plexus-component-annotations:2.1.0",
            "org.apache.maven.resolver:maven-resolver-api:1.4.1",
            "org.apache.maven.resolver:maven-resolver-util:1.4.1",
            "org.apache.maven.resolver:maven-resolver-impl:1.4.1",
            "org.apache.maven.resolver:maven-resolver-connector-basic:1.4.1",
            "org.apache.maven.resolver:maven-resolver-transport-classpath:1.4.1",
            "org.apache.maven.resolver:maven-resolver-transport-file:1.4.1",
            "org.apache.maven.resolver:maven-resolver-transport-http:1.4.1",
            "org.apache.maven:maven-resolver-provider:3.6.3"
    }
}
