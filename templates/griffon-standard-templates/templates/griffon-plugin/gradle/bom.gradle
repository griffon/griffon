/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2008-2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

generateBom {
    additionalDependencies = []
    pomConfig {
        description = projectDescription
        url         = projectWebsiteUrl
        scm {
            url = projectVcsUrl
        }
        licenses {
            license {
                name         = 'The Apache Software License, Version 2.0'
                url          = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution = 'repo'
            }
        }
        developers {
            developer {
                id   = buildBy
                name = buildBy
            }
        }
    }
}

publishing {
    publications {
        mavenBom(MavenPublication) {
            artifacts = []
        }
    }
}

publishToMavenLocal.dependsOn generateBom

gradle.taskGraph.whenReady { g ->
    def generatePom = gradle.taskGraph.allTasks.find { it.name == 'generatePomFileForMavenBomPublication' }
    def publicationExt = project.extensions.findByType(PublishingExtension)
    def pub = publicationExt.publications.findByName('mavenBom')
    generatePom?.doLast {
        copy {
            into pub.asNormalisedPublication().pomFile.parentFile
            from generateBom.outputFile
            rename generateBom.outputFile.name, 'pom-default.xml'
        }
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenBom']
    pkg {
        repo = 'griffon-plugins'
        userOrg = 'griffon'
        name = project.name
        desc = project.projectDescription
        licenses = ['Apache-2.0']
        labels = project.projectLabels.split(',')
        websiteUrl = project.projectWebsiteUrl
        issueTrackerUrl = project.projectIssueTrackerUrl
        vcsUrl = project.projectVcsUrl
        publicDownloadNumbers = true
    }
}