if (!publishJars.toBoolean()) return

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def pomConfig = {
    name project.name
    description project.projectDescription
    url project.projectWebsiteUrl
    inceptionYear project.projectInceptionYear
    scm {
        url project.projectVcsUrl
    }
    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id buildBy
            name buildBy
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                // all dependencies should use the default scope (compile) but
                // Gradle insists in using runtime as default
                asNode().dependencies.dependency.each { dep ->
                    if (dep.scope.text() == 'runtime') {
                        dep.remove(dep.scope)
                    }
                }
                asNode().children().last() + pomConfig
                asNode().appendNode('description', project.projectDescription)
            }
        }
    }
}

jar {
    manifest {
        attributes(
            'Built-By': buildBy,
            'Created-By': buildCreatedBy,
            'Build-Date': buildDate,
            'Build-Time': buildTime,
            'Build-Revision': buildRevision,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': 'griffon-framework.org',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'griffon-framework.org'
        )
    }

    metaInf {
        from rootProject.file('.')
        include 'LICENSE'
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = 'griffon-plugins'
        userOrg = 'griffon'
        name = rootProject.name
        desc = rootProject.projectDescription
        licenses = ['Apache-2.0']
        labels = ((rootProject.projectLabels.split(',') + project.projectLabels.split(',')) as List).unique()
        websiteUrl = project.projectWebsiteUrl
        issueTrackerUrl = project.projectIssueTrackerUrl
        vcsUrl = project.projectVcsUrl
        publicDownloadNumbers = true
    }
}
