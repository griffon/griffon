
[[_services]]
= Services

Services are responsible for the application logic that does not belong to a single
controller. They are meant to be treated as singletons, and are injected to MVC members by
following a naming convention. Services must be located inside the `griffon-app/services`.

Let's say you want to create a Math service. A trivial implementation of an addition
operation performed by the `MathService` would look like the following snippet:

.griffon-app/services/sample/MathService.groovy
[source,groovy,linenums,options="nowrap"]
----
package sample

import griffon.core.artifact.GriffonService

@javax.inject.Singleton
@griffon.metadata.ArtifactProviderFor(GriffonService)
class MathService {
    def addition(a, b) { a + b }
}
----

Using this service from a Controller is a straightforward task; you just have to
define an injection point and annotate it with `@javax.inject.Inject`:

.griffon-app/controllers/sample/SampleController.groovy
[source,groovy,linenums,options="nowrap"]
----
package sample

import griffon.core.artifact.GriffonController
import griffon.core.controller.ControllerAction
import griffon.inject.MVCMember
import javax.annotation.Nonnull

@griffon.metadata.ArtifactProviderFor(GriffonController)
class SampleController {
    @MVCMember @Nonnull SampleModel model

    @javax.inject.Inject
    private MathService mathService

    @ControllerAction
    void calculate(evt = null) {
        model.result = mathService.addition model.a, model.b
    }
}
----

Given that services are inherently treated as singletons, they are also automatically
registered as application event listeners. Be aware that services will be instantiated
lazily, which means that some events might not reach a particular service if it has not
been instantiated by the framework by the time of event publication. 
Using Groovy's `@Singleton` annotation on a Service class is also discouraged, as it will cause trouble with
the automatic singleton management Griffon has in place.

== Life Cycle

Services do not have a well-defined lifecycle like other MVC artifacts, because they do not
implement the `{link_griffon_mvcartifact}` interface. However, you may annotate service
methods with `@jakarta.annotation.PostConstruct` and `@jakarta.annotation.PreDestroy`.

The Griffon runtime guarantees that methods annotated with `@jakarta.annotation.PostConstruct`
will be invoked right after the instance has been created by the `{link_injector}`.
Likewise, it will invoke all methods annotated with `@jakarta.annotation.PreDestroy` when the
`{link_injector}` is closed.

NOTE: Only one method of the same class can be annotated with `@jakarta.annotation.PostConstruct`
or `@jakarta.annotation.PreDestroy`.
