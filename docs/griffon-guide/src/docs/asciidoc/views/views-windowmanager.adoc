
[[_views_windowmanager]]
= The WindowManager

Although the following API refers directly to `Swing` in all examples, it's possible to use
the `{link_window_manager}` with other toolkits such as `JavaFX`, as
there are specific `{link_window_manager}` implementations plus helper classes for those
UI toolkits too.

The `{link_window_manager}` class is responsible for keeping track of all the windows
managed by the application. It also controls how these windows are displayed (via a
pair of methods: `show`, `hide`). `{link_window_manager}` relies on an instance of
`{link_window_display_handler}` to actually show or hide a window. The default implementation
simply shows and hides windows directly; however, you can change this behavior by setting
a different implementation of `{link_window_display_handler}` on the application instance.

[[_views_windowmanager_dsl]]
== WindowManager DSL

This configuration can be set in `griffon-app/conf/Config.groovy` file; here is how it looks:

.griffon-app/conf/Config.groovy
[source,groovy,linenums,options="nowrap"]
----
windowManager {
    myWindowName = [
        show: {name, window -> ... },
        hide: {name, window -> ... }
    ]
    myOtherWindowName = [
        show: {name, window -> ... }
    ]
}
----

The name of each entry must match the value of the Window's `name:` property (if supported)
or the name used to register the Window with the `{link_window_manager}`. Each entry may
have any of the following options:

[horizontal]
show:: Used to show the window to the screen. It must be a closure that takes two
parameters: the name of the window and the window to be displayed.
hide:: Used to hide the window from the screen. It must be a closure that takes two
parameters: the name of the window and the window to be hidden.
handler:: A custom `{link_window_display_handler}` instance.

NOTE: You must use `{link_callable_with_args}` instead of closures if using the Java version
of the `Config` file.

The first two options have priority over the third one. If one is missing, then the
`{link_window_manager}` will invoke the default behavior. There is one last option
that can be used to override the default behavior provided to all windows:

.griffon-app/conf/Config.groovy
[source,groovy,linenums,options="nowrap"]
----
windowManager {
    defaultHandler = new MyCustomWindowDisplayHandler()
}
----

You can go a bit further by specifying a global show or hide behavior, as shown in the
following example:

.griffon-app/conf/Config.groovy
[source,groovy,linenums,options="nowrap"]
----
windowManager {
    defaultShow: {name, window -> ... },
    myWindowName = [
        show: {name, window -> ... },
        hide: {name, window -> ... }
    ]
    myOtherWindowName = [
        show: {name, window -> ... }
    ]
}
----

[[_views_windowmanager_starting_window]]
== Starting Window

By default, the `{link_window_manager}` picks the first available window from the managed
windows list to be the starting window. However, this behavior can be configured
by means of the WindowManager DSL. Simply specify a value for `windowManager.startingWindow`,
like this:

.griffon-app/conf/Config.groovy
[source,groovy,linenums]
----
windowManager {
    startingWindow = 'primary'
}
----

This configuration flag accepts two types of values:

 * a String that defines the name of the Window. You must make sure the Window has a
   matching name property or was attached to the `{link_window_manager}` with the
   same name.
 * a Number that defines the index of the Window in the list of managed windows.

If no match is found, then the default behavior will be executed.

[[_views_windowmanager_display_handlers]]
== Custom WindowDisplayHandlers

The following example shows how you can center on screen all managed windows:

.src/main/groovy/sample/CenteringWindowDisplayHandler.groovy
[source,groovy,linenums,options="nowrap"]
----
package sample

import org.codehaus.griffon.runtime.swing.DefaultSwingWindowDisplayHandler

import griffon.annotations.core.Nonnull
import java.awt.Window

import static griffon.swing.support.SwingUtils.centerOnScreen

class CenteringWindowDisplayHandler extends DefaultSwingWindowDisplayHandler {
    @Override
    void show(@Nonnull String name, @Nonnull Window window) {
        centerOnScreen(window)
        super.show(name, window)
    }
}
----

You can register `CenteringWindowDisplayHandler` using the WindowManager DSL. Alternatively,
you may use a <<_overview_modules, Module>> to register the class/instance.

.src/main/groovy/sample/ApplicationModule.groovy
[source,groovy,linenums,options="nowrap"]
----
package sample

import griffon.core.injection.Module
import griffon.annotations.inject.DependsOn
import griffon.swing.SwingWindowDisplayHandler
import org.codehaus.griffon.runtime.core.injection.AbstractModule
import org.kordamp.jipsy.annotations.ServiceProviderFor

import static griffon.util.AnnotationUtils.named

@DependsOn('swing')
@ServiceProviderFor(Module)
class ApplicationModule extends AbstractModule {
    @Override
    protected void doConfigure() {
        bind(SwingWindowDisplayHandler)
            .withClassifier(named('defaultWindowDisplayHandler'))
            .to(CenteringWindowDisplayHandler)
            .asSingleton()
    }
}
----

This example is equivalent to defining a `{link_window_display_handler}` for all windows.
You may target specific windows, by define multiple bindings, making sure that the name
of the classifier matches the window name. Notice the explicit dependency on the `swing` module.
If this dependency is left out, it's very likely that the `{link_window_manager}` will fail
to pick the correct `{link_window_display_handler}`.

