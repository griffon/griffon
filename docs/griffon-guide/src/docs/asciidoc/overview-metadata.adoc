
[[_overview_metadata]]
= Metadata

The Griffon runtime keeps track of useful metadata that can be consumed by applications.
The following sections describe them with more detail.

[[_overview_metadata_application]]
== Application Metadata

Access to the application's metadata file (`application.properties`) is available by
querying the `{link_metadata}` singleton. Here's a snippet of code that shows how to
setup a welcome message that displays the application's name and version:

.griffon-app/views/sample/SampleView.groovy
[source,groovy,linenums,options="nowrap"]
----
package sample

import griffon.core.artifact.GriffonView
import griffon.core.env.Metadata
import griffon.metadata.ArtifactProviderFor

import javax.inject.Inject

@ArtifactProviderFor(GriffonView)
class SampleView {
    FactoryBuilderSupport builder

    @Inject Metadata metadata

    void initUI() {
        builder.with {
            application(pack: true,
                title: application.configuration['application.title']) {
                label "Hello, I'm ${metadata['application.name']}-${metadata['application.version']}"
            }
        }
    }
}
----

There are also a few helpful methods found in `{link_metadata}`:

* `getApplicationName()` - same result as `meta['application.name']`
* `getApplicationVersion()` - same result as `meta['application.version']`

[[_overview_metadata_feature]]
== Feature

A `{link_feature}` is a boolean flag that determines if a capability is available to
the application at runtime. ``{link_feature}``s are nothing more than a `System`
property. Here's an example of a module that decides if a specific binding
should be applied over another:

.src/main/groovy/org/opendolphin/demo/DolphinModule.groovy
[source,groovy,linenums,options="nowrap"]
----
package org.opendolphin.demo

import griffon.core.env.Feature
import griffon.core.injection.Module
import org.codehaus.griffon.runtime.core.injection.AbstractModule
import org.kordamp.jipsy.ServiceProviderFor
import org.opendolphin.core.client.comm.ClientConnector
import org.opendolphin.demo.injection.HttpClientConnectorProvider
import org.opendolphin.demo.injection.InMemoryClientConnectorProvider

@ServiceProviderFor(Module)
class DolphinModule extends AbstractModule {
    @Override
    protected void doConfigure() {
        bind(ClientConnector)
            .toProvider(InMemoryClientConnectorProvider)
            .asSingleton()

        Feature.withFeature('dolphin.remote') {
            bind(ClientConnector)
                .toProvider(HttpClientConnectorProvider)
                .asSingleton()
        }
    }
}
----

The remote option can be enabled by running the application with `-Ddolphin.remote=true`,
or by adding the following entry to `griffon-app/resources/application.properties`:

.griffon-app/resources/application.properties
[source,java,linenums,options="nowrap"]
----
dolphin.remote=true
----

[[_overview_metadata_environment]]
== Application Environment

A Griffon application can run in several environments, default ones being
`DEVELOPMENT`, `TEST` and `PRODUCTION`. An application can inspect its current running
environment by means of the `{link_environment}` enum.

The following example enhances the previous one by displaying the current running
environment:

.griffon-app/views/sample/SampleView.groovy
[source,groovy,linenums,options="nowrap"]
----
package sample

import griffon.core.artifact.GriffonView
import griffon.core.env.Metadata
import griffon.core.env.Environment
import griffon.metadata.ArtifactProviderFor

import javax.inject.Inject

@ArtifactProviderFor(GriffonView)
class SampleView {
    FactoryBuilderSupport builder

    @Inject Metadata metadata
    @Inject Environment environment

    void initUI() {
        builder.with {
            application(pack: true,
                title: application.configuration['application.title']) {
                gridLayout cols: 1, rows: 2
                label "Hello, I'm ${metadata['application.name']}-${metadata['application.version']}"
                label "Current environment is ${environment}"
            }
        }
    }
}
----

The default environment is DEVELOPMENT. A different value can be specified by setting
a proper value for the `griffon.env` System property. The `{link_environment}` class
recognizes the following aliases:

 * `dev` - short for `development`.
 * `prod` - short for `production`.

You have the following options to change the environment value if using <<_buildtools_gradle,Gradle>> as build tool:

 * specify the value as a project property named `griffonEnv`.
 * specify the value in the `jvmArgs` property of the `run` task.

[[_overview_metadata_griffon_environment]]
== Griffon Environment

The `{link_griffon_environment}` gives you access to the following values:

 * Griffon version
 * Griffon build date & time (link:https://en.wikipedia.org/wiki/ISO_8601[ISO 8601, window="blank"])
 * JVM version
 * OS version

Here's an example displaying all values:

.griffon-app/views/sample/SampleView.groovy
[source,groovy,linenums,options="nowrap"]
----
package sample

import griffon.core.artifact.GriffonView
import griffon.core.env.Metadata
import griffon.core.env.Environment
import griffon.metadata.ArtifactProviderFor
import static griffon.core.env.GriffonEnvironment.*

import javax.inject.Inject

@ArtifactProviderFor(GriffonView)
class SampleView {
    FactoryBuilderSupport builder

    @Inject Metadata metadata
    @Inject Environment environment

    void initUI() {
        builder.with {
            application(pack: true,
                title: application.configuration['application.title']) {
                gridLayout cols: 1, rows: 6
                label "Hello, I'm ${metadata['application.name']}-${metadata['application.version']}"
                label "Current environment is ${environment}"
                label "Griffon version is ${getGriffonVersion()}"
                label "Build date/time is ${getBuildDateTime()}"
                label "JVM version is ${getJvmVersion()}"
                label "OS version is ${getOsVersion()}"
            }
        }
    }
}
----
