
[[_mvc]]
= The MVC Pattern

All Griffon applications operate with a basic unit called the MVC group. An MVC group
is comprised of 3 member parts: <<_models,Models>>, <<_views,Views>> and <<_controllers,Controllers>>.
However it is possible to add (or even remove) members from an MVC group by carefully
choosing a suitable configuration.

MVC groups configuration is setup in `Config.groovy` located inside `griffon-app/conf` (or
`Config.java` if using Java as main language). This file holds an entry for every MVC group
that the application has (not counting those provided by <<_addons,Addons>>).

Here's how a typical MVC group configuration looks: 

[source,groovy,linenums,options="nowrap"]
----
mvcGroups {
    // MVC Group for "sample"
    sample {
        model      = 'sample.SampleModel'
        view       = 'sample.SampleView'
        controller = 'sample.SampleController'
    }
}
----

The definition order is very important, it determines the order in which each member
will be initialized. In the previous example both `model` and `view` will be initialized
before the `controller`. Do not mistake initialization for instantiation, as initialization
relies on calling `{link_mvc_group_init}` on the group member. The name of each member is
also defined by convention, you will typically find `model`, `view`, and `controller` entries
in an MVC group configuration. These names are used to identify each member by their responsibility.
They are also used to inject member references into a particular MVC member. For example, if a
`controller` needs access to its `model` member (both defined in the same MVC group) then it simply
must define a field with matching type and name, such as

[source,groovy,linenums,options="nowrap"]
----
import griffon.core.artifact.GriffonController
import griffon.annotations.inject.MVCMember
import org.kordamp.jipsy.annotations.ServiceProviderFor
import griffon.annotations.core.Nonnull

@ServiceProviderFor(GriffonController)
class SampleController {
    @MVCMember @Nonnull SampleModel model
}
----

MVC group configurations accept a special key that defines additional configuration
for that group, as it can be seen in the following snippet:

[source,groovy,linenums,options="nowrap"]
----
mvcGroups {
    // MVC Group for "sample"
    sample {
        model      = 'sample.SampleModel'
        view       = 'sample.SampleView'
        controller = 'sample.SampleController'
    }

    // MVC Group for "foo"
    foo {
        model      = 'sample.FooModel'
        view       = 'sample.FooView'
        controller = 'sample.FooController'
        config {
            key = 'bar'
        }
    }
}
----

Values placed under this key become available to MVC members during the call to
`{link_mvc_group_init}`, as part of the arguments sent to that method. Here's how
the `FooController` can reach the key defined in the configuration:

[source,groovy,linenums,options="nowrap"]
----
@ServiceProviderFor(GriffonController)
class FooController {
    void mvcGroupInit(Map<String, Object> args) {
        println mvcGroup.configuration.config.key
    }
}
----

While being able to set additional values under this key is certainly an advantage,
it would probably be better if those values could be mutated or tweaked, probably
treating them as variables, effectively making a group configuration work as a template.
For that we'll have to discuss the `MVCGroupManager` first.

:leveloffset: 2
include::mvc-mvcmanager.adoc[]
include::mvc-mvcgroups.adoc[]
include::mvc-typed-mvcgroups.adoc[]
include::mvc-lifecycle.adoc[]
include::mvc-mvcaware-transformation.adoc[]
:leveloffset: 1
