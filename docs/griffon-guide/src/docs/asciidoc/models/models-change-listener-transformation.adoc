
[[_models_change_listener_transformation]]
= The @ChangeListener AST Transformation

The `{link_change_listener}` helps you to register ``ChangeListener``s
without so much effort. The following code

[source,groovy,linenums,options="nowrap"]
----
import griffon.transform.javafx.ChangeListener
import griffon.transform.javafx.FXObservable
import griffon.core.artifact.GriffonModel
import griffon.annotations.inject.MVCMember
import griffon.annotations.core.Nonnull

@org.kordamp.jipsy.ServiceProviderFor(GriffonModel)
class SampleModel {
    @MVCMember @Nonnull SampleController controller

    @FXObservable
    @ChangeListener(snoopAll)
    String name

    @FXObservable
    @ChangeListener({ ob, ov, nv -> controller.someAction(nv)})
    String lastname

    def snoopAll = { ob, ov, nv -> ... }
}
----

is equivalent to this one:

[source,groovy,linenums,options="nowrap"]
----
import javafx.beans.value.ChangeListener
import griffon.transform.javafx.FXObservable
import griffon.core.artifact.GriffonModel
import griffon.annotations.inject.MVCMember
import griffon.annotations.core.Nonnull

@org.kordamp.jipsy.ServiceProviderFor(GriffonModel)
class SampleModel {
    @MVCMember @Nonnull SampleController controller

    @FXObservable String name
    @FXObservable String lastname

    def snoopAll = { ob, ov, nv -> ... }

    SampleModel() {
        nameProperty().addListener(snoopAll as ChangeListener)
        lastnameProperty().addListener({ ob, ov, nv ->
            controller.someAction(nv)
        } as ChangeListener)
    }
}
----

`{link_change_listener}` accepts the following values:

 * in-place definition of a closure
 * reference of a closure property defined in the same class
 * a List of any of the previous two

`{link_change_listener}` has an additional member named `weak`. When set to `true` the generated `ChangeListener` will
be wrapped with a `WeakChangeListener`.

