
[[_controllers_context]]
= Actions and Contexts

Actions have direct access to their controller's `{link_context}` which means they can retrieve and store information
at any time. It's also possible to specify that the arguments of an action should be matched against keys that may
exist in a `{link_context}`; this enables `<<_controllers_action_handlers,ActionHandlers>>` to decorate the value
without directly affecting the `{link_context}` itself.

The following example shows a different implementation of a controller whose login action expects an instance of
`org.apache.shiro.authc.UsernamePasswordToken`. This instance has been created somewhere else and stored in the group's
context using the "_credentials_" key.

[source,groovy,linenums,options="nowrap"]
----
import griffon.core.artifact.GriffonController
import griffon.core.controller.ControllerAction
import griffon.metadata.ArtifactProviderFor
import griffon.plugins.shiro.annotation.*
import org.apache.shiro.authc.UsernamePasswordToken
impprt org.apache.shiro.subject.Subject
import javax.swing.JOptionPane

import javax.inject.Inject
import javax.inject.Named
import griffon.annotations.inject.Contextual

@ArtifactProviderFor(GriffonController)
class StrutsController {
    @Inject
    private Subject subject

    @RequiresGuest
    @ControllerAction
    void login(@Contextual @Named('credentials') UsernamePasswordToken token) {
        try {
            subject.login(token)
        } catch(Exception e) {
            JOptionPane.showMessageDialog(
                app.windowManager.findWindow('mainWindow'),
                'Invalid username and/or password',
                'Security Failure', JOptionPane.ERROR_MESSAGE)
        }
    }

    @RequiresAuthentication
    @ControllerAction
    void logout() {
        subject.logout()
    }
}
----

Because the `org.apache.shiro.authc.UsernamePasswordToken` resides in the context, it can now be shared with other
actions that need it, something that could not be done in the previous example because the `LoginCommandObject` existed
only for the action that declared it as an argument.

Another interesting thing is that the instance of `org.apache.shiro.authc.UsernamePasswordToken` could have been added
by the parent `{link_context}`, or the application's `{link_context}` (remember that contexts are hierarchical); thus
the value can be shared with more than one controller or with the whole application, as needed.

If the `{link_contextual}` argument does not have `@Named` qualifier, then the fully qualified class name of the argument's
type will be used as a key. This means that the key "org.apache.shiro.authc.UsernamePasswordToken" would be used to
search for the argument if `@Named('credentials')` were to be omitted.

It's worth noting that failure to resolve a `{link_contextual}` argument does not result in an immediate exception; if the key
could not be found in the `{link_context}`, then a `null` value will be set as the argument's value. It's the action's
job to ensure that it received the correct arguments. An alternative would be to annotate the parameter with `@Nonnull`,
in which case the `{link_action_manager}` will abort the execution if the named parameter was not found in the context, or if
its value is null.

