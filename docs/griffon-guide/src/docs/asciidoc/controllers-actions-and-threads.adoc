
[[_controllers_actions_and_threads]]
= Actions and Threads

A key aspect that you must always keep in mind is proper <<_threading,threading>>.
Often times controller actions will be bound in response to an event driven by the UI.
Those actions will usually be invoked in the same thread that triggered the event,
which would be the UI thread. When that happens you must make sure that the executed
code is short and that it quickly returns control to the UI thread. Failure to do so
may result in unresponsive applications.

The following example is the typical usage one finds out there

[source,groovy,linenums,options="nowrap"]
----
package sample

import groovy.sql.Sql
import java.awt.event.ActionEvent
import griffon.core.artifact.GriffonController

@griffon.metadata.ArtifactProviderFor(GriffonController)
class BadController {
    def model

    void badAction(ActionEvent evt = null) {
        def sql = Sql.newInstance(
            application.configuration.datasource.url,
            model.username,
            model.password,
            application.configuration.datasource.driver
        )
        model.products.clear()
        sql.eachRow("select * from products") { product ->
            model.products << [product.id, product.name, product.price]
        }
        sql.close()
    }
}
----

What's wrong with this code? It's very likely that this action is triggered by clicking
on a button, in which case its body will be executed inside the UI thread. This means
the database query will be executed on the UI thread too. The model is also updated,
once could assume the model is bound to an UI component. This update should happen inside
the UI thread, but clearly that's not what's happening here.

In order to simplify things the Griffon runtime (via the `{link_action_manager}`) assumes
by default that all actions will be invoked *outside* of the UI thread. This solves the
first problem, that of realizing a database operation on the wrong thread. The second
problem, updating the model, can be solved in the following manner

[source,groovy,linenums,options="nowrap"]
----
package sample

import groovy.sql.Sql
import java.awt.event.ActionEvent
import griffon.core.artifact.GriffonController

@griffon.metadata.ArtifactProviderFor(GriffonController)
class GoodController {
    def model

    void goodAction(ActionEvent evt = null) {                           //<1>
        def sql = Sql.newInstance(
            application.configuration.datasource.url,
            model.username,
            model.password,
            application.configuration.datasource.driver
        )

        try {
            List results = []
            sql.eachRow("select * from products") { product ->
                results << [product.id, product.name, product.price]
            }

            runInsideUIAsync {                                          //<2>
                model.products.clear()
                model.products.addAll(results)
            }
        } finally {
            sql.close()
        }
    }
}
----
<1> Executed outside the UI thread
<2> Go back inside the UI thread

There are other options at your disposal to make sure the code behaves properly according to
the specific threading rules of a particular UI toolkit. These options are covered in
the <<_threading,threading>> chapter.

The default behavior of invoking controller actions outside of the UI thread can be altered in different ways, here listed
from most specific to most generic:

 * annotating the action method with `<<_threading_annotation,@Threading>>`.
 * annotating the controller class with `<<_threading_annotation,@Threading>>`. All controller actions belonging to the
   annotated controller will use this setting.
 * specify a value under key `controller.threading.default` in the application's configuration. Accepted values and their
   equivalent `Threading.Policy` value are listed below:

[horizontal]
Threading.Policy.INSIDE_UITHREAD_SYNC:: sync, inside sync, inside uithread sync, inside_uithread_sync.
Threading.Policy.INSIDE_UITHREAD_ASYNC:: async, inside async, inside uithread async, inside_uithread_async.
Threading.Policy.OUTSIDE_UITHREAD:: outside, outside uithread, outside_uithread.
Threading.Policy.SKIP:: skip.

You may use lower case and/or upper case values. You may also use a real value of the `Threading.Policy` enum if the
application's configuration is defined using Java or Groovy code.

It's also possible to completely disable automatic UI threading management for a particular action, controller, package,
or the whole application even. Just specify a value in the application's configuration with the prefix `controller.threading`
Here are some examples:

[source,java]
----
controller.threading.com.acme.SampleController.click = false    <1>
controller.threading.org.example.SimpleController = false       <2>
controller.threading.org.another = false                        <3>
griffon.disable.threading.injection = true                      <4>
----
<1> targeted action
<2> targeted controller
<3> targeted package
<4> application wide

The [conum,data-value=1]_1_ setting disables threading management for a single action only. The [conum,data-value=2]_2_
disables threading management for all actions belonging to a single controller. [conum,data-value=3]_3_ disables threading
management for all controllers inside the `org.another` package. Finally, [conum,data-value=4]_4_ disables threading
management altogether, for the whole application.

