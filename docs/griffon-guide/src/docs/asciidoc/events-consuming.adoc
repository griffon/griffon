
[[_events_consuming]]
= Consuming events

Any artifact or class that abides to the following conventions can be registered as
an application listener, those conventions are:

 - it is a Map, a `{link_callable_with_args}` or an Object.
 - in the case of a Map, each key maps to `<EventName>`, the value must be a CallableWithArgs.
 - in the case of object, public methods whose name matches `on<EventName>` will be used
   as event handlers.
 - Objects and maps can be registered/unregistered by calling
   `addApplicationListener()`/`removeApplicationListener()` on the `EventRouter` instance.
 - CallableWithArgs event handlers must be registered with an overloaded version of
   `addApplicationListener()`/`removeApplicationListener()` that takes `<EventName>`
   as the first parameter, and the callable itself as the second parameter.

There is a global, per-application event handler that can be registered. If you want
to take advantage of this feature you must define a class that implements the
`{link_event_handler}` interface. This class must be registered with a `{link_module}`.
Lastly both Controller and Service instances are automatically registered as application
event listeners.

== Examples

These are some examples of event handlers:

Display a message right before default MVC groups are instantiated

.src/main/com/acme/ApplicationEventHandler.groovy
[source,groovy,linenums,options="nowrap"]
----
package com.acme

import griffon.core.GriffonApplication
import griffon.core.event.EventHandler

class ApplicationEventHandler implements EventHandler {
    void onBootstrapEnd(GriffonApplication application) {
        println """
            Application configuration has finished loading.
            MVC Groups will be initialized now.
        """.stripIndent(12)
    }
}
----

.src/main/groovy/com/acme/ApplicationModule.groovy
[source,groovy,linenums,options="nowrap"]
----
package com.acme

import griffon.core.event.EventHandler
import griffon.core.injection.Module
import org.codehaus.griffon.runtime.core.injection.AbstractModule
import org.kordamp.jipsy.ServiceProviderFor

@ServiceProviderFor(Module)
class ApplicationModule extends AbstractModule {
    @Override
    protected void doConfigure() {
        bind(EventHandler)
            .to(ApplicationEventHandler)
            .asSingleton()
    }
}
----

Print the name of the application plus a short message when the application is about to shut down.

.griffon-app/controller/MyController.groovy
[source,groovy,linenums,options="nowrap"]
----
package com.acme

import griffon.core.artifact.GriffonController

@griffon.metadata.ArtifactProviderFor(GriffonController)
class MyController {
    void onShutdownStart(application)
        println "${application.configuration['application.title']} is shutting down"
    }
}
----

Print a message every time the event "Foo" is published

.griffon-app/controller/MyController.groovy
[source,groovy,linenums,options="nowrap"]
----
package com.acme

import griffon.core.artifact.GriffonController

@griffon.metadata.ArtifactProviderFor(GriffonController)
class MyController {
    void mvcGroupInit(Map<String, Object> args) {
        application.eventRouter.addEventListener([
            Foo: { println 'got foo!' } as CallableWithArgs
        ])
    }

    void fooAction() {
        // do something
        application.eventRouter.publishEvent('Foo')
    }
}
----

An alternative to the previous example using a `CallableWithArgs` event handler

.griffon-app/controller/MyController.groovy
[source,groovy,linenums,options="nowrap"]
----
package com.acme

import griffon.core.artifact.GriffonController

@griffon.metadata.ArtifactProviderFor(GriffonController)
class MyController {
    void mvcGroupInit(Map<String, Object> args) {
        application.eventRouter.addEventListener('Foo',
            { println 'got foo!' } as CallableWithArgs
        ])
    }

    void fooAction() {
        // do something
        application.eventRouter.publishEvent('Foo')
    }
}
----

An alternative to the previous example using a custom event class

.griffon-app/controller/MyController.groovy
[source,groovy,linenums,options="nowrap"]
----
package com.acme

import griffon.core.artifact.GriffonController

@griffon.metadata.ArtifactProviderFor(GriffonController)
class MyController {
    void mvcGroupInit(Map<String, Object> args) {
        application.eventRouter.addListener(Foo) {
            e -> assert e instanceof Foo
        }
    }

    void fooAction() {
        // do something
        application.eventRouter.publishEvent(new MyController.Foo(this))
    }

    static class Foo extends griffon.core.Event {
        Foo(Object source) { super(source) }
    }
}
----

