/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2008-2021 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.guide'
    id 'org.ajoberstar.git-publish'
}

ext.childProjects = [
    ':griffon-annotations',
    ':griffon-annotations-core',
    ':griffon-beans-api',
    ':griffon-beans-compile',
    ':griffon-beans-impl',
    ':griffon-converter-api',
    ':griffon-converter-impl',
    ':griffon-core-api',
    ':griffon-core-compile',
    ':griffon-core-impl',
    ':griffon-core-test',
    ':griffon-groovy',
    ':griffon-groovy-compile',
    ':griffon-guice',
    ':griffon-javafx',
    ':griffon-javafx-groovy',
    ':griffon-javafx-test',
]

def sampleProjects = []
for (toolkit in ['swing', 'javafx']) {
    for (lang in ['groovy', 'java']) {
        sampleProjects << ":sample-${toolkit}-${lang}".toString()
    }
}
(ext.childProjects + sampleProjects).each { evaluationDependsOn it }

task sampleStats() {
    dependsOn sampleProjects.collect { project(it).tasks.findByName('sourceStats') }
}

asciidoctor {
    dependsOn sampleStats
    baseDirFollowsSourceDir()
    attributes = [
        toc                     : 'left',
        doctype                 : 'book',
        icons                   : 'font',
        encoding                : 'utf-8',
        sectlink                : true,
        sectanchors             : true,
        numbered                : true,
        linkattrs               : true,
        imagesdir               : 'images',
        linkcss                 : true,
        stylesheet              : 'css/style.css',
        'source-highlighter'    : 'coderay',
        'coderay-linenums-mode' : 'table',
        'griffon-version'       : project.version,
        'groovy-version'        : project.groovyVersion,
        'spock-version'         : project.spockVersion,
        'slf4j-version'         : project.slf4jVersion,
        'griffon-group'         : project.group,
        'griffon-source-url'    : project_vcs_url,
        'rootprojectdir'        : rootProject.projectDir.absolutePath,
        'gradle-griffon-version': project.version,
        'griffon-pom'           : project.version
    ]

    sources {
        include 'index.adoc'
    }

    resources {
        from file('src/resources')
        from(rootProject.file('src/media/banners/large.png')) {
            into 'images'
            rename '.*', 'griffon-banner-large.png'
        }
    }
}