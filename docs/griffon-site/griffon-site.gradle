/*
 * Copyright 2008-2015 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import groovy.json.JsonSlurper
import groovy.json.JsonParserType

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
        classpath 'org.jbake:jbake-core:2.3.2'
        classpath 'org.asciidoctor:asciidoctorj:1.5.2'
    }
}

apply plugin: 'base'
apply plugin: 'jetty'
apply plugin: 'me.champeau.jbake'

configurations {
    docs
}

dependencies {
    docs project(':griffon-guide')
}

task pluginList << {

    URL url = 'https://api.github.com/users/griffon-plugins/repos?sort=full_name'.toURL()
    def json = new JsonSlurper().setType(JsonParserType.CHARACTER_SOURCE).parse(url)

    String content = '''
        :linkattrs:
        :icons: 'font'

        [cols="2,5*^"]
        |====
    '''.stripIndent(8)

    json.each { repo ->
        content += """| link:${repo.html_url}[${repo.name}, window="_blank"]"""
        content += """pass:[<ul class="list-inline">
        <li><iframe src="http://ghbtns.com/github-btn.html?user=griffon-plugins&repo=${repo.name}&type=watch&count=true" allowtransparency="true" frameborder="0" scrolling="0" width="110" height="20"></iframe></li>
        <li><iframe src="http://ghbtns.com/github-btn.html?user=griffon-plugins&repo=${repo.name}&type=fork&count=true" allowtransparency="true" frameborder="0" scrolling="0" width="95" height="20"></iframe></li></ul>]""".stripIndent(8)
        content += """| image:https://api.bintray.com/packages/griffon/griffon-plugins/${repo.name}/images/download.svg[link="https://bintray.com/griffon/griffon-plugins/${repo.name}/_latestVersion"]"""
        content += """|Â image:http://img.shields.io/badge/docs-api-blue.svg[API, link="http://griffon-plugins.github.io/${repo.name}/"] """
        content += """| image:http://img.shields.io/travis/griffon-plugins/${repo.name}/master.svg[Build Status, link="https://travis-ci.org/griffon-plugins/${repo.name}/"] """
        content += """| image:http://img.shields.io/coveralls/griffon-plugins/${repo.name}/master.svg[Coverage Status, link="https://coveralls.io/r/griffon-plugins/${repo.name}/"] """
        content += """| image:http://img.shields.io/github/issues/${repo.full_name}.svg[Open Issues, link="https://github.com/griffon-plugins/${repo.name}/issues"]"""
        content += '\n'
    }

    content += '|====\n'

    File pluginFile = new File("${buildDir}/tmp/plugins.adoc")
    pluginFile.parentFile.mkdirs()
    pluginFile.text = content
}

jbake {
    dependsOn pluginList
    configuration['asciidoctor.attributes.export'] = true
    configuration['asciidoctor.attributes.export.prefix'] = 'jbake-'
    configuration['griffon.version.current'] = project.version
    configuration['griffon.version.previous'] = rootProject.previousVersion
    configuration['project-build-dir'] = project.buildDir.absolutePath
    configuration['project-rootdir'] = rootProject.projectDir.absolutePath
}

task(guide, dependsOn: [':griffon-guide:guide']) {
    // generates guide on the spot
}

task(copyGuide, type: Copy) {
    destinationDir = file("$buildDir/guide")
    from(tasks.getByPath(':griffon-guide:guide').destinationDir) {
        into project.version
    }
}

task(site, type: Copy, dependsOn: [jbake, copyGuide]) {
    destinationDir = file("$buildDir/site")
    from jbake.output
    from(copyGuide.destinationDir) {
        into 'guide'
    }

    doLast {
        copy {
            into file("${destinationDir}/reports/${project.version}")
            from(rootProject.tasks.clirrRootReport.reportsDir)
        }
    }
}

jettyRun {
    contextPath = ''
    webAppSourceDirectory = file(site.destinationDir)
}

task(siteRun, dependsOn: ['site', 'jettyRun']) { }

task(siteZip, type: Zip, dependsOn: site) {
    baseName = 'griffon-site'
    from site.destinationDir
}
